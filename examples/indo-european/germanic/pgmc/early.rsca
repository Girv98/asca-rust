@ Loss of Word-final non-high short vowels 
    ;; The below moves stress onto the previous syllable if the word-final vowel is stressed
    % <(..)V:[-hi, +str, -long]> > [+str] [-str] / _#

    G ~ * / _V:[-hi, -str, -long]# ;; Any preceding glide deletes
    V:[-hi, -str, -long] ~ * / _#  ;; The vowel itself deletes
    $C ~ & / _# ;; Syllable repair
#

@ Grimm's Law 
    [+cons, -son, -cont, -voice] > [+cont]
    [+cons, -son, -cont, +voice, -sg] > [-voice]
    [+cons, +voice, +sg] > [-sg]

    [+cons, -son, +voice] > [+cont] / {V:[-red], G}_{V, G}
# Chain shift of the three series of plosives.
# Voiceless plosives become fricatives
# Voiced plosives are devoiced
# Aspirated plosives become voiced plosives or fricatives.

@ Verner's Law
    [-voice, +cont] > [+voice] / V:[-stress]([+son])_
    
    s > z / #sunu_#, #ɸulno_# ;; Regularisation of nouns
    
# Voiceless fricatives are voiced when preceded by an unaccented vowel.
# Including cases where the vowel and fricative are separated by a sonorant.

@ Germanic Spirant's Law
    θ > t / O_
    O > [Alar, +cont] / V_{t:[Alar],s:[Alar]}
    θ > s / _{t, s}
#

@ Initial Stress Shift 
    % > [+str] / #_ (..) %:[+str] ;; If there is any stress, the initial syll is stressed
    %:[+str] > [-str] | #_        ;; Any non-initial stressed syll is unstressed
# Preserves unstressed words

@ Word-initial gw > b
    g:[+lab] > b / #_
#

@ W-loss
    $w > * / o:[-str]_o:[-str]
#

@ ew > ow
    e:[-stress, -long] > o / _w{#,C}
# Unstressed /ew/ > /ow/ before a consonant or word-finally e.g. *-ews > *-owz

@ E-Raising
    e:[-stress, -long] > i | _r
    j > i / i:[-str]_ | $_
# Unstressed /e/ > /i/ except before /r/ e.g. *-eþā > *-iþā
# Any unstressed coda /ej/ becomes /i:/

@ Unstressed ji > i
    j > * / _i:[-str]
    $ > * / V_i:[-str]
# Unstressed /ji/ > /i/ e.g. *legyidi > *ligiþi

@ Non-High Back Vowel Merger
    {o:[Along, Boverlong], a:[Along, Boverlong]} > ɑ:[Along, Boverlong]
# Merging of /o/ and /a/ into /ɑ/ e.g. *kápros > *hafraz