@ Loss of Word-final non-high short vowels 
    % > [+str] / _(C,0) V:[-hi, +str, -long]#
    % > [-str] / %:[+str]_#

    G > * / _V:[-hi, -str, -long]#
    V:[-hi, -str, -long] > * / _#

    $C > & / _#
    $C > & / _#
#

@ Grimm's Law 
    [+cons, -son, -cont, -voice] > [+cont]
    [+cons, -son, -cont, +voice, -sg] > [-voice]
    [+cons, +voice, +sg] > [-sg]

    [+cons, -son, +voice] > [+cont] / {V:[-red], G}_{V, G}
#

@ Verner's Law
    [-voice, +cont] > [+voice] / V:[-stress]([+son])_
#

@ Germanic Spirant's Law
    θ > t / O_
    O > [Alar, +cont] / V_{t:[Alar],s:[Alar]}
    θ > s / _{t, s}
#

@ Initial Stress Shift 
    % > [+str] / #_...V:[+str]
    %:[+str] > [-str] | #_
# Preserves unstressed words

@ Word-initial gw > b
    g:[+lab] > b / #_
#

@ W-loss
    $w > * / o:[-str]_o:[-str]
#
@ E-Raising
    e:[-stress, -long] > i | _r
    j > i / i:[-str]_ 
#
@ Unstressed ji > i
    j > * / _i:[-str]
    $ > * / V_i:[-str]
#
@ Non-High Back Vowel Merger
    {o:[Along, Boverlong], a:[Along, Boverlong]} > ɑ:[Along, Boverlong]
#